buildscript {
    ext {
        stremio_core_android_profile = "release"
        kotlinVersion = "1.7.20"
        pbandkVersion = "0.14.2"
        protobufVersion = "3.21.0"
    }

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.2.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.mozilla.rust-android-gradle:plugin:0.9.0"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.18"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "org.mozilla.rust-android-gradle.rust-android"
apply plugin: "com.google.protobuf"

android {
    compileSdkVersion 33
    ndkVersion "21.0.6113669"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 22
        versionName "1.0.21"
    }

    kotlinOptions {
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
}

cargo {
    module = "./"
    libname = "stremio_core_android"
    targets = ["arm", "arm64", "x86", "x86_64"]
    verbose = true
    profile = rootProject.ext.has("stremio_core_android_profile") ? rootProject.ext.get("stremio_core_android_profile") : "debug"
}

dependencies {
    implementation "pro.streem.pbandk:pbandk-runtime:$pbandkVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        pbandk {
            artifact = "pro.streem.pbandk:protoc-gen-pbandk-jvm:$pbandkVersion:jvm8@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove "java"
            }
            task.plugins {
                pbandk {}
            }
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == "javaPreCompileDebug" || task.name == "javaPreCompileRelease" || task.name == 'mergeDebugJniLibFolders' || task.name == 'mergeReleaseJniLibFolders') {
        task.dependsOn "cargoBuild"
    }
}
